#include "hsp3dish.as"
*Init
	screen 0,640,540// Windows版のみ有効化
    dim Score
    dim HiScore
    dim x
    dim y
    dim fight
    dim blast
    dim tamaF,10
    dim tamaX,10
    dim tamaY,10
    dim enemyF,20
    dim enemyX,20
    dim enemyY,20
    dim enemyZ,20
    dim starF,50
    dim starX,50
    dim starY,50
    Score=0
    HiScore=0
    x=(600-50)/2
    y=(400-50)-16
    fight=3
    blast=0
    HiMeter=0
    AppName="引きせかシューティング"
    celload "hidari.png",1:title "Load 1"
    celload "migi.png",2:title "Load 2"
    celload "ue.png",3:title "Load 3"
    celload "bullet.png",4:title "Load 4"
    mmload "bgm.wav",5,1:title "Load 5"
    celload "stone.png",6:title "Load 6"
    celload "main.png",7:title "Load 7"
    area_a = 500, 440, 600, 535		//6
	area_b = 160, 440, 260, 535		//5
	area_c = 040, 440, 140, 535		//4
*Start
	mmstop
    title AppName
    redraw 0
    color:boxf:clrobj
    color $00,$FF,$FF:font "",50:pos (600-50*11)/2,100:mes AppName
    color $FF,$FF,$00:font "",30:pos (600-80* 6)/2,160:mes "前回スコア:"+strf("%08d",Score)+"pt["+strf("%05dMeter]",Meter)
    color $FF,$FF,$00:font "",30:pos (600-80* 6)/2,200:mes "最高スコア:"+strf("%08d",HiScore)+"pt["+strf("%05dMeter]",HiMeter)
    color $FF,$FF,$FF:font "",16:pos (600-14*15)/2,250:mes "→ スペース・赤ボタンで開始  "
    color $FF,$FF,$FF:font "",12:pos (600-40*15)/2,380:mes "科学太郎さんのサンプルコードを使用しました。詳しくは「権利表記など」参照。  "
    color $FF,$FF,$FF:font "",12:pos (600-40*15)/2,400:mes "https://blog.goo.ne.jp/masaki_goo_2006/e/020e949bc91b811a7bbf91dd6807b1c8   "
    pos 550,330:objsize 70,70:button gosub "結果をツイート",*Tweet
    gosub *SmartKey
    redraw 1
    repeat
        stick key
        if(key & $10):break
		if IsPress6="T":break
        IsPress6 = "F"
		mtlist touch_id:touch_cnt = stat
		repeat touch_cnt
			dim t_info, 4
			mtinfo t_info, touch_id(cnt)
			if t_info(0) == 1 {
				if (t_info(1) >= area_a(0)) && (t_info(1) <= area_a(2)) && (t_info(2) >= area_a(1)) && (t_info(2) <= area_a(3)) {IsPress6 = "T"}else{IsPress6 = "F"}
			}
		loop
        await 20
    loop
    Meter=0
    mmstop
	mmplay 5
*Main
	clrobj
	Score=0
    font MSGOTHIC,50
    randomize
    repeat
        redraw 0
        stick key,%11111
        if int(tamaTrigg)>0 {tamaTrigg--}
        gosub *EnemyBirth
        gosub *StarBirth
        gosub *StarDraw
        gosub *FightDraw
        if ReturnTitle=1{ReturnTitle=0:x=(600-50)/2:y=(400-50)-16:fight=3:blast=0
        	dim starF,50
        	dim tamaF,10
        	dim enemyF,20
        	if HiMeter<Meter{HiMeter=Meter}
        	goto *Start
        }
        gosub *EnemyDraw
        gosub *TamaDraw
        gosub *TelopDraw
        gosub *SmartKey
        redraw 1
        Meter++
        if Meter>10000{Meter=Meter+1}
        await 10//Web版の場合は2
    loop
    stop
*Tweet
	exec "https://x.com/intent/post?text=引きせかｼｭｰﾃｨﾝｸﾞで"+HiScore+"点"+HiMeter+"メートルの記録を出せたよ！",16
	return
//--------------------------------------
// 自機の描画
//--------------------------------------
*FightDraw
    if(blast==0){
	    if IsPress4="T":x-=5:if(x<0):x=0:gosub *TamaBirth
	    if IsPress5="T":x+=5:if(x>550):x=550:gosub *TamaBirth
	    if IsPress6="T":gosub *TamaBirth
        if(key&1):x-=5:if(x<0):x=0
        if(key&4):x+=5:if(x>550):x=550
        if(key&16):gosub *TamaBirth
        color $00,$FF,$00:pos x,y:celput 7,0,1,1,0
    }
    else:if(blast>1){
        blast--
        color $FF,$00,$00:pos x,y
        if(blast\10<5):mes "※":else:mes "＊"
    }
    else:if(fight){
        fight--
        blast=0
        x=(600-50)/2
        y=(400-50)-16
    }
    else{
        ReturnTitle=1
    }
    return
//--------------------------------------
// 流星の発生
//--------------------------------------
*StarBirth
    if(starCycle):starCycle--:return
    foreach starF
        if(starF(cnt)==0){
            starF(cnt)=1
            starX(cnt)=rnd(600)
            starY(cnt)=0
            starS(cnt)=(rnd(2)*2+2)
            break
        }
    loop
    starCycle=3
    return
//--------------------------------------
// 流星の描画
//--------------------------------------
*StarDraw
    color $00,$00,$00:boxf
    foreach starF
        if starF(cnt){
            starY(cnt)+=2+(Meter/1500):if(starY(cnt)>=400):starF(cnt)=0:continue
            color rnd(256),rnd(256),rnd(256)
            if Meter>10000{pos starX(cnt),starY(cnt):mes "｜"}else{circle (starX(cnt)-starS(cnt)),(starY(cnt)-starS(cnt)),(starX(cnt)+starS(cnt)),(starY(cnt)+starS(cnt)),1}
        }
    loop
    return
//--------------------------------------
// 弾丸の発生
//--------------------------------------
*TamaBirth
    if(tamaTrigg):tamaTrigg--:return
    foreach tamaF
        if(tamaF(cnt)==0){
            tamaF(cnt)=1
            tamaX(cnt)=x
            tamaY(cnt)=y
            break
        }
    loop
    tamaTrigg=40
    return
//--------------------------------------
// 弾丸の描画
//--------------------------------------
*TamaDraw
    foreach tamaF
        if tamaF(cnt){
            tamaY(cnt)-=8:if(tamaY(cnt)<-50):tamaF(cnt)=0:continue
            pos tamaX(cnt),tamaY(cnt)
            celput 4,0,0.15,0.15,0
        }
    loop
    return
//--------------------------------------
// 敵機の発生
//--------------------------------------
*EnemyBirth
    if(enemyCycle):enemyCycle--:return
    foreach enemyF
        if(enemyF(cnt)==0){
            enemyF(cnt)=1
            enemyX(cnt)=rnd(600/50)*50
            enemyY(cnt)=-50
            enemyZ(cnt)=0
            break
        }
    loop
    if Meter>10000{enemyCycle=10}else{enemyCycle=30-(Meter/2000)}
    return
//--------------------------------------
// 敵機の描画
//--------------------------------------
*EnemyDraw
    foreach enemyF
        if enemyF(cnt){
            if(enemyZ(cnt)==0){
                enemyY(cnt)+=4+(Meter/3000):if(enemyY(cnt)>400)      :enemyF(cnt)=0:continue
                if FightCrash(enemyX(cnt),enemyY(cnt))  :enemyZ(cnt)=60:ScoreCalc 100:continue
                if  TamaCrash(enemyX(cnt),enemyY(cnt))  :enemyZ(cnt)=60:ScoreCalc 100:continue
                if cnt\3=0{if x<enemyX(cnt){enemyX(cnt)=enemyX(cnt)-2}else{enemyX(cnt)=enemyX(cnt)+2}}
                pos enemyX(cnt),enemyY(cnt)
                celput 6,0,0.15,0.15,0
            }
            else:if(enemyZ(cnt)>1){
                enemyZ(cnt)--
                color $FF,$00,$00:pos enemyX(cnt),enemyY(cnt)
                if(enemyZ(cnt)\10<5):mes "※":else:mes "＊"
            }
            else{
                enemyF(cnt)=0
            }
        }
    loop
    return
//--------------------------------------
// テロップの描画
//--------------------------------------
*TelopDraw
    msg=""
    repeat fight
        msg+="*"
    loop
    y(1)=0:x(1)=(20)
    y(2)=0:x(2)=(600-20)-(16*11)
    x(3)=0:y(3)=(400-20)
    font MSGOTHIC,20,1
    color $FF,$FF,$00:pos x(1),y(1):mes strf("Score:%08d",Score)
    color $FF,$FF,$00:pos 240,0:mes strf("%06d.00m",Meter)
    color $FF,$FF,$00:pos x(2),y(2):mes strf("HiScore:%08d",HiScore)
    color $00,$FF,$00:pos x(3),y(3):mes msg
    font MSGOTHIC,50
    return
//--------------------------------------
// スマホ描画区域
//--------------------------------------
*SmartKey
	color $00,$FF,$FF
 	boxf 0,420,640,540
 	pos 040,440:celput 1,0,0.75,0.75,0
 	pos 160,440:celput 2,0,0.75,0.75,0
 	pos 500,440:celput 3,0,0.75,0.75,0
	IsPress4 = "F" : IsPress5 = "F" : IsPress6 = "F"
	mtlist touch_id
	touch_cnt = stat
	repeat touch_cnt
		dim t_info, 4
		mtinfo t_info, touch_id(cnt)
		if t_info(0) == 1 {
			if (t_info(1) >= area_a(0)) && (t_info(1) <= area_a(2)) && (t_info(2) >= area_a(1)) && (t_info(2) <= area_a(3)) {IsPress6 = "T"}
			if (t_info(1) >= area_b(0)) && (t_info(1) <= area_b(2)) && (t_info(2) >= area_b(1)) && (t_info(2) <= area_b(3)) {IsPress5 = "T"}
			if (t_info(1) >= area_c(0)) && (t_info(1) <= area_c(2)) && (t_info(2) >= area_c(1)) && (t_info(2) <= area_c(3)) {IsPress4 = "T"}
		}
	loop
 	return
//--------------------------------------
// スコアの加算
//--------------------------------------
#deffunc ScoreCalc int _score_
    Score+=_score_
    if(Score>HiScore):HiScore=Score
    return
//--------------------------------------
// 自機の衝突判定
//--------------------------------------
#defcfunc FightCrash int _x_,int _y_
    if(blast==0)and(abs(x-_x_)<50)and(abs(y-_y_)<50){
        blast=60
        return 1
    }
    return 0
//--------------------------------------
// 自機弾の衝突判定
//--------------------------------------
#defcfunc TamaCrash int _x_,int _y_
    n=0
    foreach tamaF
        if tamaF(cnt){
            if(abs(tamaX(cnt)-_x_)<50)and(abs(tamaY(cnt)-_y_)<50){
                tamaF(cnt)=0
                n=1
                break
            }
        }
    loop
    return n